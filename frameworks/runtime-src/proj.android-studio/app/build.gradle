apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.biggame.sudoku2"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 20180210
        versionName "1.1.5"
        multiDexEnabled true
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "assets"
    }

    signingConfigs {

        release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':libcocos2dx')
//    implementation 'com.google.android.gms:play-services:11.8.0'
}


task cleanAssets(type: Delete) {
    delete 'assets'
}
task copyAssets(type: Copy) {
    from('../../../cocos2d-x/cocos/scripting/js-bindings/') {
        include 'script/**'
    }
    from('../../../../') {
        include 'res/**'
        include 'src/**'
        include 'main.js'
        include 'project.json'
    }
    into 'assets'
}

clean.dependsOn cleanAssets
preBuild.dependsOn copyAssets
